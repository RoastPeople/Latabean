
import with_dev from require "spec.helpers"

-- TODO: add specs for windows equivalents

describe "latann", ->
  local latann

  dev_loaded = with_dev ->
    latann = require "latabean.cmd.latann"

  same = (fn, a, b) ->
    assert.same b, fn a

  it "should normalize dir", ->
    same latann.normalize_dir, "hello/world/", "hello/world/"
    same latann.normalize_dir, "hello/world//", "hello/world/"
    same latann.normalize_dir, "", "/" -- wrong
    same latann.normalize_dir, "hello", "hello/"

  it "should parse dir", ->
    same latann.parse_dir, "/hello/world/file", "/hello/world/"
    same latann.parse_dir, "/hello/world/", "/hello/world/"
    same latann.parse_dir, "world", ""
    same latann.parse_dir, "", ""

  it "should parse file", ->
    same latann.parse_file, "/hello/world/file", "file"
    same latann.parse_file, "/hello/world/", ""
    same latann.parse_file, "world", "world"
    same latann.parse_file, "", ""

  it "convert path", ->
    same latann.convert_path, "test.lata", "test.lua"
    same latann.convert_path, "/hello/file.lata", "/hello/file.lua"
    same latann.convert_path, "/hello/world/file", "/hello/world/file.lua"

  it "calculate target", ->
    p = latann.path_to_target

    assert.same "test.lua", p "test.lata"
    assert.same "hello/world.lua", p "hello/world.lata"
    assert.same "compiled/test.lua", p "test.lata", "compiled"

    assert.same "/home/leafo/test.lua", p "/home/leafo/test.lata"
    assert.same "compiled/test.lua", p "/home/leafo/test.lata", "compiled"
    assert.same "/compiled/test.lua", p "/home/leafo/test.lata", "/compiled/"

    assert.same "latabean/hello.lua", p "latabean/hello.lata", nil, "latabean"
    assert.same "out/latabean/hello.lua", p "latabean/hello.lata", "out", "latabean"

    assert.same "out/latabean/package/hello.lua",
      p "latabean/package/hello.lata", "out", "latabean/"

    assert.same "/out/latabean/package/hello.lua",
      p "/home/leafo/latabean/package/hello.lata", "/out", "/home/leafo/latabean"

  it "should compile file text", ->
    assert.same {
      [[return print('hello')]]
    }, {
      latann.compile_file_text "print'hello'", fname: "test.lata"
    }

  describe "watcher", ->
    describe "inotify watcher", ->
      it "gets dirs", ->
        import InotifyWacher from require "latabean.cmd.watchers"
        watcher = InotifyWacher {
          {"hello.lata", "hello.lua"}
          {"cool/no.lata", "cool/no.lua"}
        }

        assert.same {
          "./"
          "cool/"
        }, watcher\get_dirs!

  describe "parse args", ->
    it "parses spec", ->
      import parse_spec from require "latabean.cmd.args"
      spec = parse_spec "lt:o:X"
      assert.same {
        X: {}
        o: {value: true}
        t: {value: true}
        l: {}
      }, spec

    it "parses arguments", ->
      import parse_arguments from require "latabean.cmd.args"
      out, res = parse_arguments {
        "ga:p"
        print: "p"
      }, {"hello", "word", "-gap"}

      assert.same {
        g: true
        a: true
        p: true
      }, out

  describe "stubbed lfs", ->
    local dirs

    before_each ->
      dirs = {}
      package.loaded.lfs = nil
      dev_loaded["latabean.cmd.latann"] = nil

      package.loaded.lfs = {
        mkdir: (dir) -> table.insert dirs, dir
        attributes: -> "directory"
      }

      latann = require "latabean.cmd.latann"

    after_each ->
      package.loaded.lfs = nil
      dev_loaded["latabean.cmd.latann"] = nil
      latann = require "latabean.cmd.latann"

    it "should make directory", ->
      latann.mkdir "hello/world/directory"
      assert.same {
        "hello"
        "hello/world"
        "hello/world/directory"
      }, dirs

